// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        //zx
        Mux16(a=x, b=false, sel=zx, out=zeroX);

        //nx
        Not16(in=zeroX, out=flippedX);
        Mux16(a=zeroX, b=flippedX, sel=nx, out=notX);

        //zy
        Mux16(a=y, b=false, sel=zy, out=zeroY);

        //ny
        Not16(in=zeroY, out=flippedY);
        Mux16(a=zeroY, b=flippedY, sel=ny, out=notY);

        //f
        Add16(a=notX, b=notY, out=addXY);
        And16(a=notX, b=notY, out=andXY);
        Mux16(a=andXY, b=addXY, sel=f, out=fRes);

        //no
        Not16(in=fRes, out=noRes);
        Mux16(a=fRes, b=noRes, sel=no, out=out, out[0..7]=outputHalf1, out[8..15]=outputHalf2, out[15]=ngSel);

        //zr
        Or8Way(in=outputHalf1, out=or8half1);
        Or8Way(in=outputHalf2, out=or8half2);
        Or(a=or8half1, b=or8half2, out=zrSel);
        Mux(a=true, b=false, sel=zrSel, out=zr);

        //ng
        Mux(a=false, b=true, sel=ngSel, out=ng);
}